buildscript {
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["MediaLibrary_kotlin_version"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def reactNativeArchitectures() {
  def value = project.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "arm64-v8a", "x86", "x86_64"]
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (project.hasProperty("newArchEnabled") && project.newArchEnabled == "true") {
  apply plugin: "com.facebook.react"
}

android {
  namespace "com.reactnativemedialibrary"

  compileSdkVersion safeExtGet("compileSdkVersion", 34)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 24)
    targetSdkVersion safeExtGet("targetSdkVersion", 34)
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  sourceSets {
    main {
      java.srcDirs = ['src/main/java']
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["MediaLibrary_kotlin_version"]
def coroutines_version = safeExtGet("coroutines", "1.7.3")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
  implementation "androidx.exifinterface:exifinterface:${safeExtGet('exifinterfaceVersion', '1.3.6')}"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
